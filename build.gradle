plugins {
  //id 'net.minecrell.licenser' version '0.3'
  id 'org.ajoberstar.grgit' version '4.1.1'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group = 'de.oceanlabs.mcp'
version = gitVersion()

ext {
    CHANGELOG = file("build/${project.name}-${project.version}-changelog.txt")
}

repositories {
    mavenCentral()
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true, tags: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name 
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

jar {
    classifier 'thin'
    manifest.attributes('Main-Class': 'de.oceanlabs.mcp.mcinjector.MCInjector')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'de.oceanlabs.mcp.mcinjector.MCInjector')
    manifest.attributes('Implementation-Version': project.version)
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives jar
    archives shadowJar
    archives sourceJar
}

def changelog = file('build/changelog.txt')
def changelog_named = CHANGELOG
if (changelog.exists()) {
    CHANGELOG.write(changelog.text)
    artifacts.archives CHANGELOG
}

dependencies {
    implementation 'org.ow2.asm:asm:9.4'
    implementation 'org.ow2.asm:asm-tree:9.4'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.google.code.gson:gson:2.10.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            if (CHANGELOG.exists())
                artifact source: CHANGELOG, classifier: 'changelog'
            pom {
                name = 'MCInjector'
                description = 'Utility for cleaning up Minecraft jar files for decompolation'
                url = 'https://github.com/ModCoderPack/MCInjector'
                scm {
                    url = 'https://github.com/ModCoderPack/MCInjector'
                    connection = 'scm:git:git://github.com/ModCoderPack/MCInjector.git'
                    developerConnection = 'scm:git:git@github.com:ModCoderPack/MCInjector.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/ModCoderPack/MCInjector/issues'
                }

                developers {
                    developer {
                        id = 'Fesh0r'
                        name = 'Fesh0r'
                    }
                    developer {
                        id = 'Searge'
                        name = 'Searge'
                    }
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }

                licenses { //TODO: Ask the guys if I can license LGPL2.1 like everything else.
                    license {
                        name = ''
                        url = ''
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('mavenPassword')) {
                credentials {
                    username = project.properties.mavenUser
                    password = project.properties.mavenPassword 
                }
                url 'https://maven.outlands.top/releases'
            } else {
                url 'file://' + project.file('repo').getAbsolutePath()
            }
        }
    }
}